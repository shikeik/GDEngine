import org.gradle.internal.jvm.Jvm

buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		if (enableGraalNative == 'true') {
			classpath "org.graalvm.buildtools.native:org.graalvm.buildtools.native.gradle.plugin:0.9.28"
		}
	}
}

plugins {
	id "application"
}

sourceSets.main.resources.srcDirs += [rootProject.file('/assets').path]
mainClassName = 'com.goldsprite.solofight.lwjgl3.Lwjgl3Launcher'
application.setMainClass(mainClassName)
eclipse.project.name = appName + '-lwjgl3'

dependencies {
	implementation project(':examples')

	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

	// 添加 ECJ 编译器 (和 Android 端版本保持一致或使用较新版)
	implementation 'org.eclipse.jdt:ecj:3.33.0'

	if (enableGraalNative == 'true') {
		implementation "io.github.berstanio:gdx-svmhelper-backend-lwjgl3:$graalHelperVersion"
	}

}

def os = System.properties['os.name'].toLowerCase()

run {
	workingDir = rootProject.file('/assets').path
	setIgnoreExitValue(true)

	if (os.contains('mac')) jvmArgs += "-XstartOnFirstThread"
}

jar {
// sets the name of the .jar file this produces to the name of the game or app, with the version after.
	archiveFileName.set("${rootProject.name}_V${projectVersion}.jar")
	destinationDirectory.set(file("${rootProject.projectDir}/outputs"))
// the duplicatesStrategy matters starting in Gradle 7.0; this setting works.
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	dependsOn configurations.runtimeClasspath
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
// these "exclude" lines remove some unnecessary duplicate files in the output JAR.
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
	dependencies {
		exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
	}
// setting the manifest makes the JAR runnable.
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
// this last step may help on some OSes that need extra instruction to make runnable JARs.
	doLast {
		file(archiveFile).setExecutable(true, false)
	}
}

// Equivalent to the jar task; here for compatibility with gdx-setup.
tasks.register('dist') {
	dependsOn 'jar'
}

distributions {
	main {
		contents {
			into('libs') {
				project.configurations.runtimeClasspath.files.findAll { file ->
					file.getName() != project.tasks.jar.outputs.files.singleFile.name
				}.each { file ->
					exclude file.name
				}
			}
		}
	}
}

startScripts.dependsOn(':lwjgl3:jar')
startScripts.classpath = project.tasks.jar.outputs.files

if (enableGraalNative == 'true') {
	apply from: file("nativeimage.gradle")
}

// 更新 exe4j 配置
tasks.register('updateExe4jOptions') {
	doFirst {
		// exe4j 配置文件路径
		def exe4jConfigFile = file("$rootProject.projectDir/outputs/SoloFight.exe4j")
		if (!exe4jConfigFile.exists() || !project.hasProperty("projectVersion")) {
			println "Exe4j 配置不存在, 跳过exe4j配置更新任务"
			return
		}
		def rootDir = rootProject.rootDir.absolutePath.replace('\\', '/')
		def outputDir = rootDir + "/outputs"
		def currentJdkPath = Jvm.current().javaHome.absolutePath.replace("\\", "/")
		def projectVersion = project.projectVersion
		def rootProjectName = rootProject.name
		def newExecutableName = "${rootProjectName}_V${projectVersion}"
		def newJarName = "${newExecutableName}.jar"

		def content = exe4jConfigFile.getText('UTF-8')
		def updatedContent = content
		//替换输出目录位置
		updatedContent = updatedContent.replaceAll(/distributionSourceDir="[^"]*"/) { match ->
			"distributionSourceDir=\"${outputDir}\""
		}
		//替换iconFile文件位置
		updatedContent = updatedContent.replaceAll(/iconFile="[^"]*"/) { match ->
			"iconFile=\"${rootDir}/assets/icon.ico\""
		}
		//替换jdk依赖路径
		updatedContent = updatedContent.replaceAll(/directory location="[^"]*"/) { match ->
			"directory location=\"${currentJdkPath}\""
		}
		//替换发布jar路径
		updatedContent = updatedContent.replaceAll(/archive location="[^"]*"/) { match ->
			"archive location=\"${outputDir}/$newJarName\""
		}

		//替换输出applicationName
		updatedContent = updatedContent.replaceAll(/application name="[^"]*"/) { match ->
			"application name=\"${rootProjectName}\""
		}
		//替换输出exeName
		updatedContent = updatedContent.replaceAll(/executable name="[^"]*"/) { match ->
			"executable name=\"${newExecutableName}\""
		}
		//替换应用主类
		updatedContent = updatedContent.replaceAll(/java mainClass="[^"]*"/) { match ->
			"java mainClass=\"${mainClassName}\""
		}
		//替换jdkVersion
		updatedContent = updatedContent.replaceAll(/minVersion="[^"]*"/) { match ->
			"minVersion=\"${jdkVersion}\""
		}
		exe4jConfigFile.write(updatedContent, 'UTF-8')

		println "✅ exe4j配置更新完毕"
	}
}
// 设置在编译 Java 前执行此任务
compileJava.dependsOn(updateExe4jOptions)
jar.dependsOn(updateExe4jOptions)


processResources {
	dependsOn ':core:copyEngineLibs'
	dependsOn ':core:packageEngineCore'
}
